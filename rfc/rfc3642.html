<pre>






Network Working Group                                            S. Legg
Request for Comments: 3642                           Adacel Technologies
Category: Informational                                     October 2003


   Common Elements of Generic String Encoding Rules (GSER) Encodings

Status of this Memo

   This memo provides information for the Internet community.  It does
   not specify an Internet standard of any kind.  Distribution of this
   memo is unlimited.

Copyright Notice

   Copyright (C) The Internet Society (2003).  All Rights Reserved.

Abstract

   The Generic String Encoding Rules (GSER) describe a human readable
   text encoding for an Abstract Syntax Notation One (ASN.1) value of
   any ASN.1 type.  Specifications making use of GSER may wish to
   provide an equivalent Augmented Backus-Naur Form (ABNF) description
   of the GSER encoding for a particular ASN.1 type as a convenience for
   implementors.  This document supports such specifications by
   providing equivalent ABNF for the GSER encodings for ASN.1 types that
   commonly occur in Lightweight Directory Access Protocol (LDAP)
   syntaxes.

Table of Contents

   1.  Introduction . . . . . . . . . . . . . . . . . . . . . . . . .  2
   2.  Conventions. . . . . . . . . . . . . . . . . . . . . . . . . .  2
   3.  Separators . . . . . . . . . . . . . . . . . . . . . . . . . .  2
   4.  ASN.1 Built-in Types . . . . . . . . . . . . . . . . . . . . .  2
   5.  ASN.1 Restricted String Types. . . . . . . . . . . . . . . . .  7
   6.  Directory ASN.1 Types. . . . . . . . . . . . . . . . . . . . .  9
   7.  Security Considerations. . . . . . . . . . . . . . . . . . . . 10
   8.  References . . . . . . . . . . . . . . . . . . . . . . . . . . 10
       8.1.  Normative References . . . . . . . . . . . . . . . . . . 10
       8.2.  Informative References . . . . . . . . . . . . . . . . . 11
   9.  Intellectual Property Notice . . . . . . . . . . . . . . . . . 12
   10. Author&#39;s Address . . . . . . . . . . . . . . . . . . . . . . . 12
   11. Full Copyright Statement . . . . . . . . . . . . . . . . . . . 13







Legg                         Informational                      [Page 1]

RFC 3642           Common Elements of GSER Encodings        October 2003


1.  Introduction

   The Generic String Encoding Rules (GSER) [7] define a human readable
   text encoding, based on ASN.1 [8] value notation, for an ASN.1 value
   of any ASN.1 type.  Specifications making use of GSER may wish to
   provide a non-normative equivalent ABNF [3] description of the GSER
   encoding for a particular ASN.1 type as a convenience for
   implementors unfamiliar with ASN.1.  This document supports such
   specifications by providing equivalent ABNF for the GSER encodings
   for ASN.1 types that commonly occur in LDAP [10] or X.500 [11]
   attribute and assertion syntaxes, as well as equivalent ABNF for the
   GSER encodings for the ASN.1 built-in types.

   The ABNF given in this document does not replace or alter GSER in any
   way.  If there is a discrepancy between the ABNF specified here and
   the encoding defined by GSER [7], then GSER is to be taken as
   definitive.

2.  Conventions

   The key words &quot;MUST&quot;, &quot;MUST NOT&quot;, &quot;REQUIRED&quot;, &quot;SHALL&quot;, &quot;SHALL NOT&quot;,
   &quot;SHOULD&quot;, &quot;SHOULD NOT&quot;, &quot;RECOMMENDED&quot;, and &quot;MAY&quot; in this document are
   to be interpreted as described in BCP 14, RFC 2119 [1].  The key word
   &quot;OPTIONAL&quot; is exclusively used with its ASN.1 meaning.

3.  Separators

   Certain separators are commonly used in constructing equivalent ABNF
   for SET and SEQUENCE types.

      sp  =  *%x20  ; zero, one or more space characters
      msp = 1*%x20  ; one or more space characters

      sep = [ &quot;,&quot; ]

   The &lt;sep&gt; rule is used in the ABNF description of the encoding for
   ASN.1 SET or SEQUENCE types where all the components are either
   OPTIONAL or DEFAULT.  It encodes to an empty string if and only if
   the immediately preceding character in the encoding is &quot;{&quot;, i.e., it
   is only empty for the first optional component actually present in
   the SET or SEQUENCE value being encoded.

4.  ASN.1 Built-in Types

   This section describes the GSER encoding of values of the ASN.1
   built-in types, except for the restricted character string types.





Legg                         Informational                      [Page 2]

RFC 3642           Common Elements of GSER Encodings        October 2003


   The &lt;BIT-STRING&gt; rule describes the GSER encoding of values of the
   BIT STRING type without a named bit list.

      BIT-STRING = bstring / hstring

   If the number of bits in a BIT STRING value is a multiple of four the
   &lt;hstring&gt; form of &lt;BIT-STRING&gt; MAY be used.  Otherwise, the &lt;bstring&gt;
   form of &lt;BIT-STRING&gt; is used.  The &lt;bstring&gt; rule encodes each bit as
   the character &quot;0&quot; or &quot;1&quot; in order from the first bit to the last bit.
   The &lt;hstring&gt; rule encodes each group of four bits as a hexadecimal
   number where the first bit is the most significant.  An odd number of
   hexadecimal digits is permitted.

      hstring           = squote *hexadecimal-digit squote %x48 ; &#39;...&#39;H
      hexadecimal-digit = %x30-39 /  ; &quot;0&quot; to &quot;9&quot;
                          %x41-46    ; &quot;A&quot; to &quot;F&quot;

      bstring           = squote *binary-digit squote %x42  ; &#39;...&#39;B
      binary-digit      = &quot;0&quot; / &quot;1&quot;

      squote            =  %x27  ; &#39; (single quote)

   The &lt;BOOLEAN&gt; rule describes the GSER encoding of values of the
   BOOLEAN type.

      BOOLEAN = %x54.52.55.45 /   ; &quot;TRUE&quot;
                %x46.41.4C.53.45  ; &quot;FALSE&quot;

   The &lt;CHARACTER-STRING&gt; rule describes the GSER encoding of values of
   the associated type for the unrestricted CHARACTER STRING type.

      CHARACTER-STRING = &quot;{&quot; sp id-identification msp Identification &quot;,&quot;
                             sp id-data-value msp OCTET-STRING
                             sp &quot;}&quot;

      id-identification = %x69.64.65.6E.74.69.66.69.63.61.74.69.6F.6E
                             ; &quot;identification&quot;
      id-data-value     = %x64.61.74.61.2D.76.61.6C.75.65 ; &quot;data-value&quot;

      Identification = ( id-syntaxes &quot;:&quot; Syntaxes ) /
                       ( id-syntax &quot;:&quot; OBJECT-IDENTIFIER ) /
                       ( id-presentation-context-id &quot;:&quot; INTEGER ) /
                       ( id-context-negotiation &quot;:&quot;
                            ContextNegotiation ) /
                       ( id-transfer-syntax &quot;:&quot; OBJECT-IDENTIFIER ) /
                       ( id-fixed &quot;:&quot; NULL )





Legg                         Informational                      [Page 3]

RFC 3642           Common Elements of GSER Encodings        October 2003


      id-syntaxes                = %x73.79.6E.74.61.78.65.73
                                      ; &quot;syntaxes&quot;
      id-syntax                  = %x73.79.6E.74.61.78 ; &quot;syntax&quot;
      id-presentation-context-id = %x70.72.65.73.65.6E.74.61.74.69.6F.6E
                                      %x2D.63.6F.6E.74.65.78.74.2D.69.64
                                      ; &quot;presentation-context-id&quot;
      id-context-negotiation     = %x63.6F.6E.74.65.78.74.2D.6E.65.67.6F
                                      %x74.69.61.74.69.6F.6E
                                      ; &quot;context-negotiation&quot;
      id-transfer-syntax         = %x74.72.61.6E.73.66.65.72.2D.73.79.6E
                                      %x74.61.78 ; &quot;transfer-syntax&quot;
      id-fixed                   = %x66.69.78.65.64 ; &quot;fixed&quot;

      Syntaxes = &quot;{&quot; sp id-abstract msp OBJECT-IDENTIFIER &quot;,&quot;
                     sp id-transfer msp OBJECT-IDENTIFIER
                     sp &quot;}&quot;
      id-abstract = %x61.62.73.74.72.61.63.74 ; &quot;abstract&quot;
      id-transfer = %x74.72.61.6E.73.66.65.72 ; &quot;transfer&quot;

      ContextNegotiation = &quot;{&quot; sp id-presentation-context-id msp
                                     INTEGER &quot;,&quot;
                               sp id-transfer-syntax msp
                                     OBJECT-IDENTIFIER
                               sp &quot;}&quot;

   The &lt;INTEGER&gt; rule describes the GSER encoding of values of the
   INTEGER type without a named number list.  The &lt;INTEGER-0-MAX&gt; rule
   describes the GSER encoding of values of the constrained type INTEGER
   (0..MAX).  The &lt;INTEGER-1-MAX&gt; rule describes the GSER encoding of
   values of the constrained type INTEGER (1..MAX).

      INTEGER         = &quot;0&quot; / positive-number / (&quot;-&quot; positive-number)
      INTEGER-0-MAX   = &quot;0&quot; / positive-number
      INTEGER-1-MAX   = positive-number
      positive-number = non-zero-digit *decimal-digit
      decimal-digit   = %x30-39  ; &quot;0&quot; to &quot;9&quot;
      non-zero-digit  = %x31-39  ; &quot;1&quot; to &quot;9&quot;

   The &lt;EMBEDDED-PDV&gt; rule describes the GSER encoding of values of the
   associated type for the EMBEDDED PDV type.

      EMBEDDED-PDV = &quot;{&quot; sp id-identification msp Identification &quot;,&quot;
                         sp id-data-value msp OCTET-STRING
                         sp &quot;}&quot;

   The &lt;EXTERNAL&gt; rule describes the GSER encoding of values of the
   associated type for the EXTERNAL type.




Legg                         Informational                      [Page 4]

RFC 3642           Common Elements of GSER Encodings        October 2003


      EXTERNAL = &quot;{&quot; [ sp id-direct-reference msp
                             OBJECT-IDENTIFIER &quot;,&quot; ]
                     [ sp id-indirect-reference msp INTEGER &quot;,&quot; ]
                     [ sp id-data-value-descriptor msp
                             ObjectDescriptor &quot;,&quot; ]
                       sp id-encoding msp Encoding
                       sp &quot;}&quot;

      id-direct-reference      = %x64.69.72.65.63.74.2D.72.65.66.65.72
                                    %x65.6E.63.65
                                    ; &quot;direct-reference&quot;
      id-indirect-reference    = %x69.6E.64.69.72.65.63.74.2D.72.65.66
                                    %x65.72.65.6E.63.65
                                    ; &quot;indirect-reference&quot;
      id-data-value-descriptor = %x64.61.74.61.2D.76.61.6C.75.65.2D.64
                                    %x65.73.63.72.69.70.74.6F.72
                                    ; &quot;data-value-descriptor&quot;
      id-encoding              = %x65.6E.63.6F.64.69.6E.67
                                    ; &quot;encoding&quot;

      Encoding = ( id-single-ASN1-type &quot;:&quot; Value ) /
                 ( id-octet-aligned &quot;:&quot; OCTET-STRING ) /
                 ( id-arbitrary &quot;:&quot; BIT-STRING )

      id-single-ASN1-type = %x73.69.6E.67.6C.65.2D.41.53.4E.31.2D.74.79
                               %x70.65
                               ; &quot;single-ASN1-type&quot;
      id-octet-aligned    = %x6F.63.74.65.74.2D.61.6C.69.67.6E.65.64
                               ; &quot;octet-aligned&quot;

      id-arbitrary        = %x61.72.62.69.74.72.61.72.79
                               ; &quot;arbitrary&quot;

   The &lt;Value&gt; rule is defined by GSER [7].  It represents the GSER
   encoding of a single value of the ASN.1 type identified by the
   direct-reference and/or indirect-reference components.

   The &lt;NULL&gt; rule describes the GSER encoding of values of the NULL
   type.

      NULL = %x4E.55.4C.4C  ; &quot;NULL&quot;

   The &lt;OBJECT-IDENTIFIER&gt; rule describes the GSER encoding of values of
   the OBJECT IDENTIFIER type.

      OBJECT-IDENTIFIER = numeric-oid / descr
      numeric-oid       = oid-component 1*( &quot;.&quot; oid-component )
      oid-component     = &quot;0&quot; / positive-number



Legg                         Informational                      [Page 5]

RFC 3642           Common Elements of GSER Encodings        October 2003


   An OBJECT IDENTIFIER value is encoded using either the dotted decimal
   representation or an object descriptor name, i.e., &lt;descr&gt;.  The
   &lt;descr&gt; rule is described in RFC 2252 [4].  An object descriptor name
   is potentially ambiguous and should be used with care.

   The &lt;OCTET-STRING&gt; rule describes the GSER encoding of values of the
   OCTET STRING type.

      OCTET-STRING = hstring

   The octets are encoded in order from the first octet to the last
   octet.  Each octet is encoded as a pair of hexadecimal digits where
   the first digit corresponds to the four most significant bits of the
   octet.  If the hexadecimal string does not have an even number of
   digits, the four least significant bits in the last octet are assumed
   to be zero.

   The &lt;REAL&gt; rule describes the GSER encoding of values of the REAL
   type.

      REAL = &quot;0&quot;                    ; zero
             / PLUS-INFINITY        ; positive infinity
             / MINUS-INFINITY       ; negative infinity
             / realnumber           ; positive base 10 REAL value
             / ( &quot;-&quot; realnumber )   ; negative base 10 REAL value
             / real-sequence-value  ; non-zero base 2 or 10 REAL value

      PLUS-INFINITY  = %x50.4C.55.53.2D.49.4E.46.49.4E.49.54.59
                          ; &quot;PLUS-INFINITY&quot;

      MINUS-INFINITY = %x4D.49.4E.55.53.2D.49.4E.46.49.4E.49.54.59
                          ; &quot;MINUS-INFINITY&quot;

      realnumber = mantissa exponent
      mantissa   = (positive-number [ &quot;.&quot; *decimal-digit ])
                   / ( &quot;0.&quot; *(&quot;0&quot;) positive-number )
      exponent   = &quot;E&quot; ( &quot;0&quot; / ([ &quot;-&quot; ] positive-number))

      real-sequence-value = &quot;{&quot; sp id-mantissa msp INTEGER &quot;,&quot;
                                sp id-base msp ( &quot;2&quot; / &quot;10&quot; ) &quot;,&quot;
                                sp id-exponent msp INTEGER sp &quot;}&quot;
      id-mantissa         = %x6D.61.6E.74.69.73.73.61 ; &quot;mantissa&quot;
      id-base             = %x62.61.73.65             ; &quot;base&quot;
      id-exponent         = %x65.78.70.6F.6E.65.6E.74 ; &quot;exponent&quot;

   A value of the REAL type MUST be encoded as &quot;0&quot; if it is zero.





Legg                         Informational                      [Page 6]

RFC 3642           Common Elements of GSER Encodings        October 2003


   The &lt;RELATIVE-OID&gt; rule describes the GSER encoding of values of the
   RELATIVE-OID type.

      RELATIVE-OID = oid-component *( &quot;.&quot; oid-component )

5.  ASN.1 Restricted String Types

   This section describes the GSER encoding of values of the ASN.1
   restricted character string types.  The characters of a value of a
   restricted character string type are always encoded as a UTF-8
   character string between double quotes.  For some of the ASN.1 string
   types, this requires a translation to or from the UTF-8 encoding.
   Some of the ASN.1 string types permit only a subset of the characters
   representable in UTF-8.  Any double quote characters in the character
   string, where allowed by the character set, are escaped by being
   repeated.

   The &lt;UTF8String&gt; rule describes the GSER encoding of values of the
   UTF8String type.  The characters of this string type do not require
   any translation before being encoded.

      UTF8String        = StringValue
      StringValue       = dquote *SafeUTF8Character dquote

      dquote            = %x22 ; &quot; (double quote)

      SafeUTF8Character = %x00-21 / %x23-7F /   ; ASCII minus dquote
                          dquote dquote /       ; escaped double quote
                          %xC0-DF %x80-BF /     ; 2 byte UTF-8 character
                          %xE0-EF 2(%x80-BF) /  ; 3 byte UTF-8 character
                          %xF0-F7 3(%x80-BF)    ; 4 byte UTF-8 character

   The &lt;NumericString&gt;, &lt;PrintableString&gt;, &lt;VisibleString&gt;,
   &lt;ISO646String&gt;, &lt;IA5String&gt;, &lt;GeneralizedTime&gt; and &lt;UTCTime&gt; rules
   describe the GSER encoding of values of the correspondingly named
   ASN.1 types.  The characters of these string types are compatible
   with UTF-8 and do not require any translation before being encoded.
   The GeneralizedTime and UTCTime types use the VisibleString character
   set, but have a strictly defined format.

      NumericString        = dquote *(decimal-digit / space) dquote
      space                = %x20









Legg                         Informational                      [Page 7]

RFC 3642           Common Elements of GSER Encodings        October 2003


      PrintableString      = dquote *PrintableCharacter dquote
      PrintableCharacter   = decimal-digit / space
                             / %x41-5A ; A to Z
                             / %x61-7A ; a to z
                             / %x27-29 ; &#39; ( )
                             / %x2B-2F ; + , - . /
                             / %x3A    ; :
                             / %x3D    ; =
                             / %x3F    ; ?

      ISO646String         = VisibleString
      VisibleString        = dquote *SafeVisibleCharacter dquote
      SafeVisibleCharacter = %x20-21
                             / %x23-7E ; printable ASCII minus dquote
                             / dquote dquote   ; escaped double quote

      IA5String            = dquote *SafeIA5Character dquote
      SafeIA5Character     = %x00-21 / %x23-7F ; ASCII minus dquote
                             / dquote dquote   ; escaped double quote

      century = 2(%x30-39) ; &quot;00&quot; to &quot;99&quot;
      year    = 2(%x30-39) ; &quot;00&quot; to &quot;99&quot;
      month   =   ( %x30 %x31-39 ) ; &quot;01&quot; (January) to &quot;09&quot;
                / ( %x31 %x30-32 ) ; &quot;10&quot; to &quot;12&quot;
<font color='red'><s>
      day     =   ( %x30 %x31-39 )    ; &quot;01&quot; to &quot;09&quot;
                / ( %x31-32 %x30-39 ) ; &quot;10&quot; to &quot;29&quot;
                / ( %x32 %x30-31 )    ; &quot;30&quot; to &quot;31&quot;
      </s></font></pre><div style='overflow:auto;border:dashed;margin:8px;padding:24px;background-color:#FFFFAF;'><b><i>The preceding text has been updated by Technical <a href='https://www.rfc-editor.org/errata/eid5136'>Errata 5136</a> to instead read:</i></b><pre style='margin:20px'>

      day     =   ( %x30 %x31-39 )    ; &quot;01&quot; to &quot;09&quot;
                / ( %x31-32 %x30-39 ) ; &quot;10&quot; to &quot;29&quot;
                / ( %x33 %x30-31 )    ; &quot;30&quot; to &quot;31&quot;
</pre><i><b>Errata Notes: </b>

The day value incorrectly includes values 01 to 09, 10 to 29 and then 20 to 21.  The last field should instead allow for values 30 to 31.

</i></div><pre>
hour    = ( %x30-31 %x30-39 ) / ( %x32 %x30-33 ) ; &quot;00&quot; to &quot;23&quot;
      minute  = %x30-35 %x30-39                        ; &quot;00&quot; to &quot;59&quot;
      second  =   ( %x30-35 %x30-39 )  ; &quot;00&quot; to &quot;59&quot;
                / ( %x36 %x30 )        ; &quot;60&quot; (a leap second)

      UTCTime         = dquote year month day hour minute [ second ]
                           [ %x5A / u-differential ] dquote
      u-differential  = ( &quot;-&quot; / &quot;+&quot; ) hour minute

      GeneralizedTime = dquote century year month day hour
                           [ minute [ second ] ] [ fraction ]
                           [ %x5A / g-differential ] dquote
      fraction        = ( &quot;.&quot; / &quot;,&quot; ) 1*(%x30-39)
      g-differential  = ( &quot;-&quot; / &quot;+&quot; ) hour [ minute ]

   The &lt;BMPString&gt; and &lt;UniversalString&gt; rules describe the GSER
   encoding of values of the BMPString and UniversalString types
   respectively.  BMPString (UCS-2) and UniversalString (UCS-4) values
   are translated into UTF-8 [6] character strings before being encoded
   according to &lt;StringValue&gt;.




Legg                         Informational                      [Page 8]

RFC 3642           Common Elements of GSER Encodings        October 2003


      BMPString       = StringValue
      UniversalString = StringValue

   The &lt;TeletexString&gt;, &lt;T61String&gt;, &lt;VideotexString&gt;, &lt;GraphicString&gt;,
   &lt;GeneralString&gt; and &lt;ObjectDescriptor&gt; rules describe the GSER
   encoding of values of the correspondingly named ASN.1 types.  Values
   of these string types are translated into UTF-8 character strings
   before being encoded according to &lt;StringValue&gt;.  The
   ObjectDescriptor type uses the GraphicString character set.

      TeletexString    = StringValue
      T61String        = StringValue
      VideotexString   = StringValue
      GraphicString    = StringValue
      GeneralString    = StringValue
      ObjectDescriptor = GraphicString

6.  Directory ASN.1 Types

   This section describes the GSER encoding of values of selected ASN.1
   types defined for LDAP and X.500.  The ABNF rule names beginning with
   uppercase letters describe the GSER encoding of values of the ASN.1
   type with the same name.

      AttributeType  = OBJECT-IDENTIFIER

   The characters of a DirectoryString are translated into UTF-8
   characters as required before being encoded between double quotes
   with any embedded double quotes escaped by being repeated.

      DirectoryString = StringValue /
                        ( id-teletexString   &quot;:&quot; TeletexString ) /
                        ( id-printableString &quot;:&quot; PrintableString ) /
                        ( id-bmpString       &quot;:&quot; BMPString ) /
                        ( id-universalString &quot;:&quot; UniversalString ) /
                        ( id-uTF8String      &quot;:&quot; UTF8String )

      id-teletexString   = %x74.65.6C.65.74.65.78.53.74.72.69.6E.67
                              ; &quot;teletexString&quot;
      id-printableString = %x70.72.69.6E.74.61.62.6C.65
                              %x53.74.72.69.6E.67 ; &quot;printableString&quot;
      id-bmpString       = %x62.6D.70.53.74.72.69.6E.67 ; &quot;bmpString&quot;
      id-universalString = %x75.6E.69.76.65.72.73.61.6C
                              %x53.74.72.69.6E.67 ; &quot;universalString&quot;
      id-uTF8String      = %x75.54.46.38.53.74.72.69.6E.67
                                 ; &quot;uTF8String&quot;





Legg                         Informational                      [Page 9]

RFC 3642           Common Elements of GSER Encodings        October 2003


   The &lt;RDNSequence&gt; rule describes the GSER encoding of values of the
   RDNSequence type, which is syntactically equivalent to the
   DistinguishedName and LocalName types.  The &lt;RDNSequence&gt; rule
   encodes a name as an LDAPDN character string between double quotes.
   The character string is first derived according to the
   &lt;distinguishedName&gt; rule in Section 3 of RFC 2253 [5], and then it is
   encoded between double quotes with any embedded double quotes escaped
   by being repeated.

      DistinguishedName = RDNSequence
      LocalName         = RDNSequence
      RDNSequence       = dquote *SafeUTF8Character dquote

   The &lt;RelativeDistinguishedName&gt; rule describes the GSER encoding of
   values of the RelativeDistinguishedName type that are not part of an
   RDNSequence value.  The &lt;RelativeDistinguishedName&gt; rule encodes an
   RDN as a double quoted string containing the RDN as it would appear
   in an LDAPDN character string.  The character string is first derived
   according to the &lt;name-component&gt; rule in Section 3 of RFC 2253 [5],
   and then any embedded double quote characters are escaped by being
   repeated.  This resulting string is output between double quotes.

      RelativeDistinguishedName = dquote *SafeUTF8Character dquote

   The &lt;ORAddress&gt; rule encodes an X.400 address as an IA5 character
   string between double quotes.  The character string is first derived
   according to Section 4.1 of RFC 2156 [2], and then any embedded
   double quotes are escaped by being repeated.  This resulting string
   is output between double quotes.

      ORAddress = dquote *SafeIA5Character dquote

7. Security Considerations

   This document contains an alternative description of parts of the
   Generic String Encoding Rules, but does not replace or alter GSER in
   any way.  For the full security implications of using GSER, see the
   Security Considerations section for GSER [7].

8.  References

8.1.  Normative References

   [1]  Bradner, S., &quot;Key words for use in RFCs to Indicate Requirement
        Levels&quot;, BCP 14, RFC 2119, March 1997.

   [2]  Kille, S., &quot;MIXER (Mime Internet X.400 Enhanced Relay): Mapping
        between X.400 and RFC 822/MIME&quot;, RFC 2156, January 1998.



Legg                         Informational                     [Page 10]

RFC 3642           Common Elements of GSER Encodings        October 2003


   [3]  Crocker, D. and P. Overell, &quot;Augmented BNF for Syntax
        Specifications: ABNF&quot;, RFC 2234, November 1997.

   [4]  Wahl, M., Coulbeck, A., Howes, T. and S. Kille, &quot;Lightweight
        Directory Access Protocol (v3): Attribute Syntax Definitions&quot;,
        RFC 2252, December 1997.

   [5]  Wahl, M., Kille, S. and T. Howes, &quot;Lightweight Directory Access
        Protocol (v3): UTF-8 String Representation of Distinguished
        Names&quot;, RFC 2253, December 1997.

   [6]  Yergeau, F., &quot;UTF-8, a transformation format of ISO 10646&quot;, RFC
        2279, January 1998.

   [7]  Legg, S., &quot;Generic String Encoding Rules (GSER) for ASN.1
        Types&quot;, RFC 3641, October 2003.

   [8]  ITU-T Recommendation X.680 (07/02) | ISO/IEC 8824-1:2002
        Information technology - Abstract Syntax Notation One (ASN.1):
        Specification of basic notation

8.2.  Informative References

   [9]  Hovey, R. and S. Bradner, &quot;The Organizations Involved in the
        IETF Standards Process&quot;, BCP 11, RFC 2028, October 1996.

   [10] Hodges, J. and R. Morgan, &quot;Lightweight Directory Access Protocol
        (v3): Technical Specification&quot;, RFC 3377, September 2002.

   [11] ITU-T Recommendation X.500 (1993) | ISO/IEC 9594-1:1994,
        Information Technology - Open Systems Interconnection - The
        Directory: Overview of concepts, models and services



















Legg                         Informational                     [Page 11]

RFC 3642           Common Elements of GSER Encodings        October 2003


9. Intellectual Property Notice

   The IETF takes no position regarding the validity or scope of any
   intellectual property or other rights that might be claimed to
   pertain to the implementation or use of the technology described in
   this document or the extent to which any license under such rights
   might or might not be available; neither does it represent that it
   has made any effort to identify any such rights.  Information on the
   IETF&#39;s procedures with respect to rights in standards-track and
   standards-related documentation can be found in BCP-11.  Copies of
   claims of rights made available for publication and any assurances of
   licenses to be made available, or the result of an attempt made to
   obtain a general license or permission for the use of such
   proprietary rights by implementors or users of this specification can
   be obtained from the IETF Secretariat.

   The IETF invites any interested party to bring to its attention any
   copyrights, patents or patent applications, or other proprietary
   rights which may cover technology that may be required to practice
   this standard.  Please address the information to the IETF Executive
   Director.

10.  Author&#39;s Address

   Steven Legg
   Adacel Technologies Ltd.
   250 Bay Street
   Brighton, Victoria 3186
   AUSTRALIA

   Phone: +61 3 8530 7710
   Fax:   +61 3 8530 7888
   EMail: steven.legg@adacel.com.au


















Legg                         Informational                     [Page 12]

RFC 3642           Common Elements of GSER Encodings        October 2003


11.  Full Copyright Statement

   Copyright (C) The Internet Society (2003).  All Rights Reserved.

   This document and translations of it may be copied and furnished to
   others, and derivative works that comment on or otherwise explain it
   or assist in its implementation may be prepared, copied, published
   and distributed, in whole or in part, without restriction of any
   kind, provided that the above copyright notice and this paragraph are
   included on all such copies and derivative works.  However, this
   document itself may not be modified in any way, such as by removing
   the copyright notice or references to the Internet Society or other
   Internet organizations, except as needed for the purpose of
   developing Internet standards in which case the procedures for
   copyrights defined in the Internet Standards process must be
   followed, or as required to translate it into languages other than
   English.

   The limited permissions granted above are perpetual and will not be
   revoked by the Internet Society or its successors or assignees.

   This document and the information contained herein is provided on an
   &quot;AS IS&quot; basis and THE INTERNET SOCIETY AND THE INTERNET ENGINEERING
   TASK FORCE DISCLAIMS ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING
   BUT NOT LIMITED TO ANY WARRANTY THAT THE USE OF THE INFORMATION
   HEREIN WILL NOT INFRINGE ANY RIGHTS OR ANY IMPLIED WARRANTIES OF
   MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.

Acknowledgement

   Funding for the RFC Editor function is currently provided by the
   Internet Society.



















Legg                         Informational                     [Page 13]

</pre>